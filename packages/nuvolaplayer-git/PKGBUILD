# Maintainer: Patrick Burroughs (Celti) <celti@celti.name>

# Unofficial repository (x86_64 and i686) available:
# Build scripts at: https://repo.celti.name/nuvolaplayer/
#
# # Both the packages and the database are signed:
# # `pacman-key --recv-keys 123C3F8B058A707F86643316FA682BD8910CF4EA`
# # `pacman-key --lsign-key 123C3F8B058A707F86643316FA682BD8910CF4EA`
# [nuvolaplayer]
# SigLevel = Required TrustedOnly
# Server = https://repo.celti.name/archlinux/$repo/$arch

pkgname=nuvolaplayer-git
pkgver=3.0.0.r38.g4d5feb9
pkgrel=3

arch=('i686' 'x86_64')
license=('GPL')
pkgdesc='Cloud music integration for your Linux desktop.'
url='https://tiliado.eu/nuvolaplayer/'

source=("$pkgname::git+https://github.com/tiliado/nuvolaplayer"
        'nuvola-app-git-1.0.0.template')
sha256sums=('SKIP'
            '8e61d52d4c5bae4ad6e62c0e579dcb0e4053ae5dd4315085b37339deaff01cea')

depends=('diorite-git' 'webkit2gtk' 'libarchive')
makedepends=('git' 'python' 'vala')
optdepends=('nuvola-app-amazon-cloud-player-git:   Amazon Cloud Player integration'
            'nuvola-app-bandcamp-git:              Bandccamp integration'
            'nuvola-app-deezer-git:                Deezer integration'
            'nuvola-app-google-play-music-git:     Google Play Music integration'
            'nuvola-app-groove-git:                Microsoft Groove Music integration'
            'nuvola-app-jango-git:                 Jango integration'
            'nuvola-app-kexp-git:                  KEXP-FM (Seattle 90.3) integration'
            'nuvola-app-logitech-media-server-git: Logitech Media Server integration'
            'nuvola-app-mixcloud-git:              Mixcloud integration'
            'nuvola-app-owncloud-music-git:        OwnCloud Music integration'
            'nuvola-app-plex-git:                  Plex Media integration'
            'nuvola-app-soundcloud-git:            SoundCloud integration'
            'nuvola-app-spotify-git:               Spotify integration'
            'nuvola-app-tunein-git:                TuneIn integration'
            'nuvola-app-yandex-music-git:          Yandex.Music integration'
            'nuvola-app-google-calendar-git:       (Experimental) Google Calendar integration'
            'nuvola-app-8tracks-git:               8tracks.com integration (not functional)'
            'nuvola-app-hype-machine-git:          Hype Machine integration (not functional)'
            'nuvola-app-pandora-git:               Pandora integration (not functional)' )

pkgver() {
	cd "$pkgname"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$pkgname"
	python ./waf configure --prefix=/usr
	python ./waf build
}

package() {
	cd "$pkgname"
	python ./waf install --no-system-hooks --destdir="${pkgdir}"

	# Installl makepkg template for nuvola-app-*-git integrations.
	install -Dm644 "${srcdir}/nuvola-app-git-1.0.0.template" "${pkgdir}/usr/share/makepkg-template/nuvola-app-git-1.0.0.template"
	ln -s nuvola-app-git-1.0.0.template "${pkgdir}/usr/share/makepkg-template/nuvola-app-git.template"
}
