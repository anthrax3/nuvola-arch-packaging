# Maintainer: Patrick Burroughs (Celti) <celti@celti.name>

pkgname=nuvolaplayer-mse-git
pkgver=3.1.0.r60.ge7c77ba
pkgrel=1

arch=('i686' 'x86_64')
license=('GPL')
pkgdesc='Cloud music integration for your Linux desktop.'
url='https://tiliado.eu/nuvolaplayer/'

source=("$pkgname::git+https://github.com/tiliado/nuvolaplayer"
        'nuvola-app-git-1.5.1.template')
sha256sums=('SKIP'
            '8dd752d67a0088eeac5d3a64cfc6b929c41b935e0cc9867c09d1043783cf0479')

provides=('nuvolaplayer=3.1.0')
conflicts=('nuvolaplayer')

depends=('diorite0.3' 'webkit2gtk-mse' 'libarchive')
makedepends=('git' 'python' 'vala')
optdepends=(
	'nuvola-app-8tracks-git:               8tracks.com integration'
	'nuvola-app-amazon-cloud-player-git:   Amazon Cloud Player integration'
	'nuvola-app-bandcamp-git:              Bandccamp integration'
	'nuvola-app-deezer-git:                Deezer integration'
	'nuvola-app-google-play-music-git:     Google Play Music integration'
	'nuvola-app-groove-git:                Microsoft Groove Music integration'
	'nuvola-app-jango-git:                 Jango integration'
	'nuvola-app-kexp-git:                  KEXP-FM (Seattle 90.3) integration'
	'nuvola-app-logitech-media-server-git: Logitech Media Server integration'
	'nuvola-app-mixcloud-git:              Mixcloud integration'
	'nuvola-app-owncloud-music-git:        OwnCloud Music integration'
	'nuvola-app-plex-git:                  Plex Media integration'
	'nuvola-app-soundcloud-git:            SoundCloud integration'
	'nuvola-app-spotify-git:               Spotify integration'
	'nuvola-app-tunein-git:                TuneIn integration'
	'nuvola-app-yandex-music-git:          Yandex.Music integration'
	'nuvola-app-google-calendar-git:       Google Calendar integration (experimental)'
	'nuvola-app-hype-machine-git:          Hype Machine integration (not functional)'
	'nuvola-app-pandora-git:               Pandora integration (not functional)'
)

pkgver() {
	cd "$pkgname"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$pkgname"
	python ./waf configure --with-apps-alpha --prefix=/usr
	python ./waf build
}

package() {
	cd "$pkgname"
	python ./waf install --no-system-hooks --destdir="${pkgdir}"

	# Installl makepkg template for nuvola-app-*-git integrations.
	install -Dm644 "${srcdir}/nuvola-app-git-1.5.1.template" "${pkgdir}/usr/share/makepkg-template/nuvola-app-git-1.5.1.template"
	ln -s nuvola-app-git-1.5.1.template "${pkgdir}/usr/share/makepkg-template/nuvola-app-git.template"
}
